#!/usr/bin/perl

use strict;

use FindBin;
use lib "$FindBin::RealBin/lib";

use Carrier::ComfortZoneII::Interface;
use IO::File;
use IO::Socket::IP;
use JSON;

###############################################################################

my $config  = $ENV{CZ2_CONFIG} || "$ENV{HOME}/.cz2";
my $connect = $ENV{CZ2_CONNECT};
my $zones   = $ENV{CZ2_ZONES};

unless ($connect and $zones) {
  my $CONFIG = IO::File->new ($config)
    or die "Error reading $config: $!\n";

  while (<$CONFIG>) {
    chomp;
    s/\s*#.*$//;
    s/^\s+|\s+$//g;

    next unless (length $_);

    my ($var, $val) = /^(\S+)\s*=\s*(.+)$/
      or die "Invalid configuration setting at $config line $.\n";

    if    ($var eq "connect") { $connect ||= $val }
    elsif ($var eq "zones")   { $zones   ||= $val }
    else  { die "Invalid parameter '$var' at $config line $.\n"; }
  }
}

my $zone_count;
my @zone_names;

if ($zones =~ /^\d+$/) {

  $zone_count = $zones;
  @zone_names = map { "Zone $_" } (1..$zone_count);

} else {

  my @zones   = split /\s*,\s*/, $zones;
  $zone_count = scalar @zones;
  @zone_names = map { "$_. $zones[$_-1]" } (1..$zone_count);
}

###############################################################################

my $usage = <<USAGE;
Usage: $0

HIGH-LEVEL COMMANDS

  status

    Print an overview of the current system status.

  set_system <option>...

    Set system-wide options. The options are one or more key=value
    pairs from the table below. Example: set_system mode=auto

    Key   Values
    ----  ----------------------------
    mode  heat, cool, auto, eheat, off
    fan   auto, always_on
    all   on, off

    If ALL mode is enabled, the zone 1 settings will apply to all
    zones and the settings for the other zones will be ignored.

  set_zone <"all"/zone/list/range> <option>...

    Set zone options. The first parameter can be "all", a zone number,
    a comma-separated list (1,3,5), or a range (1-3). The options are
    one or more key=value pairs from the table below.

    Key   Values    Description
    ----  --------  ---------------------------------
    heat  <number>  Sets the heating setpoint
    cool  <number>  Sets the cooling setpoint
    temp  on, off   Enables "temporary setpoint" mode
    hold  on, off   Enables HOLD mode
    out   on, off   Enables OUT mode

    In temporary setpoint mode, the zone will keep the current
    setpoint until the next scheduled change, and then automatically
    resume the pre-programmed schedule. In HOLD mode, the zone will
    ignore all scheduled changes and keep the current setpoint
    forever.

    To change setpoints, you must enable either temporary setpoint
    mode or HOLD mode. Otherwise the controller will quickly revert to
    the currently scheduled program.

    Keep in mind that if ALL mode is enabled, the zone 1 settings will
    apply to all zones and the settings for the other zones will be
    ignored.

  set_time <date> <time>

    Set the current system time. The first parameter is a day
    abbreviation (e.g. Mon) and the second parameter is a time in
    either 12-hour (3:00pm) or 24-hour (15:00) format.

  show_schedules

    Print a chart of all of the programmed schedules, including start
    times and setpoints.

  set_schedule <"all"/zone/list/range> <"all"/day/list/range>
               <"all"/program/list> <option>...

    Program the schedules on the controller. The first parameter
    refers to the zone and it can be "all", a zone number, a
    comma-separated list (1,3,5), or a range (1-3).

    The second parameter refers to the day of the week and it can be
    "all", a day abbreviation (Mon), a comma-separated list
    (Mon,Wed,Fri), or a range (Mon-Wed).

    The third parameter refers to the program. Valid programs are
    Wake, Day, Eve, and Sleep. This parameter can be "all", a program
    name, or a comma-separated list.

    The remaining parameters can be one or more of the following:

      start=<time>
      heat=<heating setpoint>
      cool=<cooling setpoint>
      out

    The start time can be specified as either 12-hour (3:00pm) or
    24-hour (15:00). Setting a heating or cooling setpoint will
    disable OUT mode. Examples:

                   Zone Day     Program   Options
                   ---- ------- --------- -------------------
      set_schedule 1    mon     eve       start=19:00 heat=68
      set_schedule 1-3  all     day,sleep heat=64 cool=72
      set_schedule all  mon-fri day       start=9:00am out

LOW-LEVEL COMMANDS

  monitor

    Passively monitor all serial traffic and print each frame
    observed. Runs until cancelled with Ctrl-C.

  read <dest> <table> <row>

    Send a read request for one row and print the data received.
    <dest> is the destination device id, <table> is the table number
    without the leading 0, and <row> is the row number.

    Example: to read the heating and cooling setpoints from the master
    controller, use: read 1 1 16

  write <dest> <table> <row> <byte>.<byte>.<byte>...

    Send a write request to overwrite the data in one complete row.
    You must include the correct number of bytes, starting from byte
    3. Do not include the 3-byte prefix (0, table number, row number)
    in the data section.

    Example: to set the current time on the master controller to
    Thursday 12:30pm, use: write 1 1 18 4.12.30.00

  write_byte <dest> <table> <row> <index>:<byte> <index>:<byte> ...

    Send a write request to change one or more individual bytes in a
    data row. This is done by reading the data for this row, changing
    the specified bytes, then writing back the complete row.

    Example: to set "all" mode (where the zone 1 setpoints are used
    for all zones), use: write_byte 1 1 12 15:1

  read_all [<dest> <table>]

    Print a dump of the data in all known rows in the given table on
    the specified destination device. Valid combinations are:

      9  9 - Panel settings and data
      1  1 - Master controller settings and data
      1 10 - Master controller schedules

    With no arguments, print a dump of the data in all of these.

  status_data

    Print the status information in JSON format.

  decode_raw <data>

    Decode the "raw" value in the status_data output, and use it to
    print a dump in the same format as read_all.

  status_from_raw <data>

    Decode the "raw" value in the status_data output, and use it as
    input to the "status" command. This can be used to redisplay the
    human-readable status from a saved snapshot.
USAGE

sub check_numeric {
  #
  # Make sure all arguments are valid numeric values.
  #
  for (@_) {
    die "Missing or invalid argument\n" unless (/^\d+$/ and $_ <= 255);
  }
}

sub dtr_args {
  #
  # Parse the standard destination, table, and row arguments.
  #
  my $dest  = shift @ARGV;
  my $table = shift @ARGV;
  my $row   = shift @ARGV;

  check_numeric ($dest, $table, $row);

  return ($dest, $table, $row);
}

sub parse_time {
  my ($value) = @_;

  $value =~ /^(\d?\d):(\d\d)([ap]m)?$/i
    or return;

  my $hour   = $1 + 0;
  my $minute = $2 + 0;
  my $ampm   = $3;

  $hour += 12 if ($ampm =~ /p/i and $hour <  12);
  $hour -= 12 if ($ampm =~ /a/i and $hour == 12);

  ($hour   >= 0 and $hour   <= 23) or return;
  ($minute >= 0 and $minute <= 59) or return;

  my $time = [$hour, $minute];

  if    ($hour ==  0) { $hour  = 12; $ampm  = "am"; }
  elsif ($hour == 12) {              $ampm  = "pm"; }
  elsif ($hour >  12) { $hour -= 12; $ampm  = "pm"; }
  else                {              $ampm  = "am"; }

  push @$time, sprintf "%d:%02d%s", $hour, $minute, $ampm;

  return $time;
}

sub kv_args {
  #
  # Parse key=value style arguments.
  #
  my ($spec) = @_;
  my $args;

  while (my $arg = shift @ARGV) {
    my ($key, $value) = split /=/, $arg, 2;

    my $check = $spec->{$key}
      or die "Invalid parameter '$key'\n";

    if (ref ($check) eq "HASH") {

      $value = $check->{$value};

      defined ($value)
        or die "Invalid value for parameter '$key'\n";

      $args->{$key} = $value;

    } elsif ($check eq "numeric") {

      die "Invalid value for parameter '$key'\n"
        unless ($value =~ /^\d+$/ and $value <= 255);

      $args->{$key} = $value;

    } elsif ($check eq "onoff") {

      $args->{$key} =
        $value eq "on"  ? 1 :
        $value eq "off" ? 0 :
        die "Invalid value for parameter '$key'\n";

    } elsif ($check eq "time") {

      $args->{$key} = parse_time ($value)
        or die "Invalid value for parameter '$key'\n";

    } else {

      die "Internal error";

    }
  }

  return $args;
}

sub multi_arg {
  #
  # Parse one "all"/list/range-type argument. The valid values should
  # all be lowercase.
  #
  my ($name, $values, $allow_range) = @_;

  my $arg = lc shift @ARGV
    or die "Missing or invalid $name argument\n";

  my %map = map { $values->[$_] => $_ } (0..$#{$values});
  my $re  = join "|", sort @$values;
  my %args;

  my @specs = split /,/, $arg;
  for (@specs) {
    if ($_ eq "all") {
      $args{$_} = 1 for (0..$#{$values});
    } elsif (/^($re)-($re)$/ and $map{$2} >= $map{$1} and $allow_range) {
      $args{$_} = 1 for ($map{$1}..$map{$2});
    } elsif (/^($re)$/) {
      $args{$_} = 1 for ($map{$_});
    } else {
      die "Invalid $name $_\n";
    }
  }

  return map { [$_, $values->[$_]] } sort keys %args;
}

sub check_no_more_args {
  #
  # Make sure there are no extra command-line arguments;
  #
  die "Too many arguments\n" if @ARGV;
}

###############################################################################

sub populate_data {
  my ($c, $data, $dest, $table, $row) = @_;

  my $key = "$table.$row";
  return $data->{$key} if $data->{$key};

  my $f = $c->send_with_reply ($dest, "read", 0, $table, $row);
  $data->{$key} = $f->{data};

  return $data->{$key};
}

sub set_field {
  my ($c, $data, $dest, $table, $row, $field, $value) = @_;

  populate_data ($c, $data, $dest, $table, $row);
  $data->{"$table.$row"}->[$field] = $value;
}

sub set_bit_field {
  my ($c, $data, $dest, $table, $row, $field, $bit, $value) = @_;

  populate_data ($c, $data, $dest, $table, $row);

  my $ref  = \($data->{"$table.$row"}->[$field]);
  my $mask = ~(1 << $bit);

  $$ref &= $mask;
  $$ref |= ($value << $bit);
}

sub write_all {
  my ($c, $dest, $data) = @_;

  for my $key (sort keys %$data) {
    my $row = $data->{$key};

    my $f = $c->send_with_reply ($dest, "write", @$row);

    my $reply = $f->{data}->[0];
    unless ($reply == 0) {
      die "Reply code $reply writing row $row\n";
    }
  }
}

###############################################################################

my $action = shift;

my $c = Carrier::ComfortZoneII::Interface->new
  (
   connect => $connect,
   zones   => $zone_count,
  );

###############################################################################

if ($action eq "" or $action eq "help") {

  die $usage;

} elsif ($action eq "monitor") {

  check_no_more_args;

  while (my $f = $c->get_frame) {
    $c->print_frame ($f);
  }

} elsif ($action eq "read") {

  my ($dest, $table, $row) = dtr_args;
  check_no_more_args;

  my $f = $c->send_with_reply ($dest, "read", 0, $table, $row);

  printf "%s\n", join ".", @{$f->{data}};

} elsif ($action eq "read_all") {

  my $dest  = shift;
  my $table = shift;
  my $target;

  if (length $dest) {
    check_numeric ($dest, $table);
    check_no_more_args;

    $target = "$dest.$table";
  }

  my @messages;
  if ($target eq "9.9"  or not $target) { push @messages, [9, 0,  9, $_] for (1..5)  }
  if ($target eq "1.1"  or not $target) { push @messages, [1, 0,  1, $_] for (1..34) }
  if ($target eq "1.10" or not $target) { push @messages, [1, 0, 10, $_] for (1..56) }

  for my $message (@messages) {
    my ($dest, @data) = @$message;

    my $f = $c->send_with_reply ($dest, "read", @data);
    $c->print_frame ($f);
  }

} elsif ($action eq "write") {

  my ($dest, $table, $row) = dtr_args;

  my $data  = shift;
  my @data  = split /\./, $data;

  check_numeric (@data);
  check_no_more_args;

  my $f = $c->send_with_reply ($dest, "write", 0, $table, $row, @data);
  $c->print_reply ($f);

} elsif ($action eq "write_byte") {

  my ($dest, $table, $row) = dtr_args;
  my @set;

  while (my $arg = shift) {
    my ($idx, $val) = split /:/, $arg;
    check_numeric ($idx, $val);

    if ($idx < 3) {
      die "Invalid index $idx\n";
    }

    push @set, [$idx, $val];
  }

  die "Not enough arguments\n" unless @set;

  my $f      = $c->send_with_reply ($dest, "read", 0, $table, $row);
  my @data   = @{$f->{data}};
  my $change = 0;

  for my $set (@set) {
    my ($idx, $val) = @$set;

    if ($idx > $#data) {
      die "Maximum index for this row is $#data\n";
    }

    my $old = $data[$idx];

    if ($old == $val) {
      print "Byte $idx is already set to $val\n";
      next;
    }

    print "Changing byte $idx from $old to $val\n";

    $data[$idx] = $val;
    $change     = 1;
  }

  if ($change) {
    my $f = $c->send_with_reply ($dest, "write", @data);
    $c->print_reply ($f);
  } else {
    print "No changes needed\n";
  }

} elsif ($action =~ /^status(_from_raw)?$/) {

  my $raw;
  if ($1) {
    $raw = shift
      or die "Missing argument\n";
  }

  check_no_more_args;

  my $status    = $c->get_status_data ($raw);

  my $mode      = $status->{system_mode};
  my $effective = $status->{effective_mode};
  my $all_mode  = $status->{all_mode};
  my $fan_mode  = $status->{fan_mode};
  my $fan_on    = $status->{fan} ? "On" : "Off";

  my $is_auto   = $mode eq "Auto";
  my $is_heat   = $effective =~ /Heat/;

  $mode .= " ($effective)" if $is_auto;

  my $active;
  if ($status->{compressor}) {
    if ($is_heat) {
      $active  = $status->{reverse} ? "Defrosting" : "Heat On";
      $active .= " [AUX HEAT]" if $status->{aux_heat};
    } else {
      $active  = "Cool On";
    }
  } elsif ($status->{aux_heat}) {
    $active = "AUX Heat On";
  } else {
    $active = $is_heat ? "Heat Off" : "Cool Off";
  }

  print "System Time: $status->{system_time}\n";
  print "Ambient:     Outside $status->{outside_temp}° / Indoor humidity $status->{zone1_humidity}%\n";
  print "Air Handler: $status->{air_handler_temp}°, Fan $fan_on, $active\n";
  print "Mode:        $mode, Fan $fan_mode\n";
  print "\n";

  my $name_len = (sort { $a <=> $b } map { length $_ } @zone_names)[-1] + 1;

  for my $zone (1..$zone_count) {
    my $zonedata = $status->{zones}->[$zone-1];

    my $all  = ($zone == $all_mode)   ? " [ALL]"  : "";
    my $temp = $zonedata->{temporary} ? " [TEMP]" : "";
    my $hold = $zonedata->{hold}      ? " [HOLD]" : "";

    my $cool = $zonedata->{out} ? "OUT" : sprintf "%2d°", $zonedata->{cool_setpoint};
    my $heat = $zonedata->{out} ? "OUT" : sprintf "%2d°", $zonedata->{heat_setpoint};

    my $cool_msg = "Cool setpoint $cool";
    my $heat_msg = "Heat setpoint $heat";
    my $setpoint =
      $is_auto ? "$cool_msg, $heat_msg" :
      $is_heat ? $heat_msg : $cool_msg;

    printf "%-${name_len}s Temperature %2d°, Damper %3s%%, %s%s%s\n",
      $zone_names[$zone-1] . ":",
      @{$status->{zones}->[$zone-1]}{qw(temperature damper_position)},
      $setpoint,
      $hold || $temp,
      $all;
  }

} elsif ($action eq "status_data") {

  check_no_more_args;

  my $status = $c->get_status_data;

  print JSON->new->canonical->encode ($status), "\n";

} elsif ($action eq "decode_raw") {

  my $raw = shift
    or die "Missing argument\n";

  check_no_more_args;

  my @frames = $c->decode_raw ($raw);

  for my $f (@frames) {
    $c->print_frame ($f);
  }

} elsif ($action eq "set_system") {

  my $spec =
    {
     mode => {
              heat      => 0,
              cool      => 1,
              auto      => 2,
              eheat     => 3,
              off       => 4,
             },
     fan  => {
              auto      => 0,
              always_on => 1,
             },
     all  => "onoff",
    };

  my $args = kv_args ($spec);
  my $data = {};

  unless (keys %$args) {
    die "No options specified\n";
  }

  while (my ($key, $value) = each %$args) {

    if    ($key eq "mode") { set_field     ($c, $data, 1, 1, 12,  4,    $value) }
    elsif ($key eq "fan")  { set_bit_field ($c, $data, 1, 1, 17,  3, 2, $value) }
    elsif ($key eq "all")  { set_field     ($c, $data, 1, 1, 12, 15,    $value) }

    else  { die "Internal error" }

  }

  write_all ($c, 1, $data);
  print "Ok\n";

} elsif ($action eq "set_zone") {

  my @zone_args = multi_arg ("zone", [1..$zone_count], 1);
  my @zones     = map { $_->[1] } @zone_args;

  my $spec =
    {
     heat => "numeric",
     cool => "numeric",
     temp => "onoff",
     hold => "onoff",
     out  => "onoff",
    };

  my $args = kv_args ($spec);
  my $data = {};

  unless (keys %$args) {
    die "No options specified\n";
  }

  for my $zone (@zones) {
    while (my ($key, $value) = each %$args) {

      if    ($key eq "heat") { die "Invalid heat setpoint for zone $zone\n" if ($value < 55 or $value > 74) }
      if    ($key eq "cool") { die "Invalid cool setpoint for zone $zone\n" if ($value < 64 or $value > 99) }

      if    ($key eq "heat") { set_field     ($c, $data, 1, 1, 16, 10+$zone,          $value) }
      elsif ($key eq "cool") { set_field     ($c, $data, 1, 1, 16,  2+$zone,          $value) }
      elsif ($key eq "temp") { set_bit_field ($c, $data, 1, 1, 12,  9,       $zone-1, $value) }
      elsif ($key eq "hold") { set_bit_field ($c, $data, 1, 1, 12, 10,       $zone-1, $value) }
      elsif ($key eq "out")  { set_bit_field ($c, $data, 1, 1, 12, 12,       $zone-1, $value) }

      else  { die "Internal error" }

    }
  }

  write_all ($c, 1, $data);
  print "Ok\n";

} elsif ($action eq "set_time") {

  my @days = qw(sun mon tue wed thu fri sat);
  my %days = map { $days[$_] => $_ } (0..$#days);

  my $day_arg  = shift;
  my $time_arg = shift;

  my $day = $days{lc $day_arg};
  defined $day
    or die "Invalid day\n";

  my $time = parse_time ($time_arg)
    or die "Invalid time\n";

  check_no_more_args;

  my $data =
    {
     "1.18" => [0, 1, 18, $day, $time->[0], $time->[1], 0],
    };

  write_all ($c, 1, $data);
  print "Ok\n";

} elsif ($action eq "show_schedules") {

  check_no_more_args;

  my $data = {};
  for my $row (1..($zone_count*7)) {
    populate_data ($c, $data, 1, 10, $row);
  }

  print "         | WAKE            | DAY             | EVE             | SLEEP\n";
  print "Zone Day | Start Cool Heat | Start Cool Heat | Start Cool Heat | Start Cool Heat\n";
  print "---------+-----------------+-----------------+-----------------+----------------\n";

  my $format = "   %1d %s | %2d:%02d  %3s  %3s | %2d:%02d  %3s  %3s | %2d:%02d  %3s  %3s | %2d:%02d  %3s  %3s\n";
  my @dow    = qw(Sun Mon Tue Wed Thu Fri Sat);

  for my $zone (1..$zone_count) {
    for my $dow (0..$#dow) {
      my $idx = (7*($zone-1))+1 + $dow;
      my $row = $data->{"10.$idx"};

      my @fields = ($zone, $dow[$dow]);

      for my $program (0..3) {
        my $hour   = $row->[ 3+$program];
        my $minute = $row->[ 7+$program];
        my $heat   = $row->[11+$program];
        my $cool   = $row->[15+$program];

        if ($heat & 0x80) {
          $heat = "OUT";
          $cool = "OUT";
        } else {
          $heat .= "°";
          $cool .= "°";
        }

        push @fields, $hour, $minute, $cool, $heat;
      }

      printf $format, @fields;
    }
  }

} elsif ($action eq "set_schedule") {

  my @zone_args    = multi_arg ("zone",    [1..$zone_count],                  1);
  my @day_args     = multi_arg ("day",     [qw(sun mon tue wed thu fri sat)], 1);
  my @program_args = multi_arg ("program", [qw(wake day eve sleep)],          0);

  my $spec =
    {
     start => "time",
     heat  => "numeric",
     cool  => "numeric",
     out   => { "" => 1 },
    };

  my $args = kv_args ($spec);

  unless (keys %$args) {
    die "No options specified\n";
  }

  if ($args->{out} and ($args->{heat} or $args->{cool})) {
    die "You cannot set both 'out' and heating/cooling setpoints\n";
  }

  my $data  = {};

  my @zones    = map { $_->[1] } @zone_args;
  my @days     = map { $_->[0] } @day_args;
  my @programs = map { $_->[0] } @program_args;

  my %out_val =
    (
     0 => 196,
     1 => 188,
     2 => 196,
     3 => 188,
    );

  for my $zone (@zones) {
    for my $dow (@days) {
      my $idx = (7*($zone-1))+1 + $dow;
      my $row = populate_data ($c, $data, 1, 10, $idx);

      for my $program (@programs) {
        my $f_hour   =  3+$program;
        my $f_minute =  7+$program;
        my $f_heat   = 11+$program;
        my $f_cool   = 15+$program;

        while (my ($key, $value) = each %$args) {
          if ($key eq "start") {
            my ($hour, $minute) = @$value;

            $row->[$f_hour]   = $hour;
            $row->[$f_minute] = $minute;
          }

          elsif ($key eq "heat") { $row->[$f_heat]   = $value;             }
          elsif ($key eq "cool") { $row->[$f_cool]   = $value;             }
          elsif ($key eq "out")  { $row->[$f_heat]   = $out_val{$program}; }

          else  { die "Internal error\n"; }
        }
      }
    }
  }

  write_all ($c, 1, $data);
  print "Ok\n";

} else {

  die "Invalid action: $action\n";

}
